// @sylefeb 2021
// MIT license, see LICENSE_MIT in Silice repo root
// https://github.com/sylefeb/Silice

import('ddr_clock.v')

$$if VERILATOR then
import('../common/verilator_data.v') // this is a feature supported by Silice
                                     // verilator framework to access raw data
                                     // stored in a 'data.raw' file
$$end

$$if not nBytes then
$$ nBytes = 1
$$end

algorithm spiflash_qspi(
  input  uint8 send,
  input  uint1 trigger,
  input  uint1 send_else_read,
  input  uint1 qspi,
  output uint8 read,
  output uint1 clk,
  inout  uint1 io0,
  inout  uint1 io1,
  inout  uint1 io2,
  inout  uint1 io3,
) {
  uint1 dc(0);
  uint8 sending(0);
  uint1 osc(0);
  uint1 enable(0);

  ddr_clock ddr(clock <: clock, enable <:: enable, ddr_clock :> clk);

  always {

    io0.oenable = send_else_read;
    io1.oenable = send_else_read & qspi;
    io2.oenable = send_else_read & qspi;
    io3.oenable = send_else_read & qspi;

    read      = {read[0,4],io3.i,io2.i,io1.i,io0.i};
    io0.o     = ~osc ? sending[0,1] : sending[4,1];
    io1.o     = ~osc ? sending[1,1] : sending[5,1];
    io2.o     = ~osc ? sending[2,1] : sending[6,1];
    io3.o     = ~osc ? sending[3,1] : sending[7,1];

    sending   = (~osc | ~enable) ? send : sending;
    osc       = ~trigger ? 1b0 : ~osc;
    enable    = trigger;
  }
}

algorithm spiflash_rom(
  input   uint1  in_ready,
  input   uint24 addr,
  output  uint$nBytes*8$ rdata,
  output  uint1  busy(1),
  // QSPI flash
  output  uint1  sf_csn(1),
  output  uint1  sf_clk,
  inout   uint1  sf_io0,
  inout   uint1  sf_io1,
  inout   uint1  sf_io2,
  inout   uint1  sf_io3,
) <autorun> {

  uint32 sendvec(0); //_ 38h (QPI enable)

  spiflash_qspi spiflash(
    clk     :> sf_clk,
    io0    <:> sf_io0,
    io1    <:> sf_io1,
    io2    <:> sf_io2,
    io3    <:> sf_io3,
  );

$$if SIMULATION then
  uint10 wait(16);
$$else
  uint10 wait(1023);
$$end
  uint4  four(0);
  uint3  stage(0);
  uint3  after(1);
  uint2  init(2b11);
$$if nBytes > 1 then
  uint$nBytes$ rdata_done(0);
$$end
$$if SIMULATION then
  uint32 cycle(0);
$$if VERILATOR then
  verilator_data vdta(clock <: clock);
$$end
$$end
  always {

    spiflash.qspi = ~init[1,1]; // qpi activated after first command

    switch (stage)
    {
      case 0: {
$$if ICARUS then
        // this is necessary for icarus as spiflash.qspi is otherwise 1bz
        spiflash.qspi    = reset ? 0 : spiflash.qspi;
        spiflash.trigger = reset ? 0 : spiflash.trigger;
        spiflash.send    = 0;
$$end
        stage = wait == 0 ? after : 0; // NOTE == 0 could be reduced (initial wait is wide)
        wait  = wait - 1;
      }
      case 1: {
        four    = {init[0,1],~init[0,1],2b00};
        sendvec = (init == 2b01 ? {8hEB,addr} : 24h0)
                | (init == 2b00 ? {addr,8h00} : 24h0)
                | (init[1,1]    ? 32b00000000000100010001000000000000 : 24h0);
               //                ^^^^^^^^^^ produces 38h when not in QPI
        spiflash.send_else_read = 1; // sending
$$if nBytes > 1 then
        rdata_done = init[1,1] ? 1 : $1<<(nBytes-1)$;
$$end
        // start sending?
        if (in_ready | init[1,1]) {
$$if SIMULATION then
          //__display("[%d](%d) spiflash [1] START qspi:%d init:%b",cycle,cycle>>1,spiflash.qspi,init);
$$if VERILATOR then
          vdta.addr = addr;
          //__display("[%d] spiflash, read @%h (%b) init:%b",cycle,addr,rdata_done,init);
$$end
$$end
          busy                  = 1;
          sf_csn                = 0;
          stage                 = 2;
        }
      }
      case 2: {
$$if ICARUS then
        //__display("[%d] spiflash [2] qspi:%d init:%b send:%b",cycle,spiflash.qspi,init,sendvec[24,8]);
$$end
        spiflash.trigger        = 1;
        spiflash.send           = sendvec[24,8];
        sendvec                 = sendvec << 8;
        stage                   = 0; // wait
        wait                    = 0; //_ 2 cycles
        after                   = four[0,1] ? 3 : 2;
        four                    = four >> 1;
      }
      case 3: {
        sf_csn                  =  init[1,1]; // not sending anything if in init
        spiflash.trigger        = ~init[1,1];
        // send dummy
        spiflash.send           = 8b00100000; // requests continuous read
        stage                   = 0; // wait
        wait                    = 1; //_ 3 cycles
        after                   = 4;
      }
      case 4: {
        spiflash.send_else_read = 0;
        stage                   = 0; // wait
        wait                    = 0; //_ 2 cycles
        after                   = 5;
      }
      case 5: {
$$if nBytes == 1 then
  $$if SIMULATION then
  $$if VERILATOR then
        rdata                   = vdta.data;  // from 'data.raw'
  $$else
        rdata                   = cycle[0,8]; // DEBUG
  $$end
  $$else
        rdata                   = spiflash.read;
  $$end
        sf_csn                  = 1;
        spiflash.trigger        = 0;
        busy                    = 0;
        init                    = {1b0,init[1,1]};
        stage                   = 1; // return to start stage
$$else
  $$if SIMULATION then
  $$if VERILATOR then
        rdata                   = {vdta.data[0,8],rdata[8,$(nBytes-1)*8$]};  // from 'data.raw'
        vdta.addr               = rdata_done[0,1] ? vdta.addr : (vdta.addr + 1);
  $$else
        rdata                   = {cycle[0,8],    rdata[8,$(nBytes-1)*8$]}; // DEBUG
  $$end
  $$else
        rdata                   = {spiflash.read, rdata[8,$(nBytes-1)*8$]};
  $$end
        sf_csn                  =  rdata_done[0,1];
        spiflash.trigger        = ~rdata_done[0,1];
        busy                    = ~rdata_done[0,1];
        init                    = {1b0,init[1,1]};
        stage                   = rdata_done[0,1] ? 1 : 5; // return to start stage when done
        rdata_done              = rdata_done >> 1;
$$end
$$if SIMULATION then
      //__display("[%d](%d) spiflash [5] DONE (%h)",cycle,cycle>>1,rdata);
$$end
      }
    }
$$if SIMULATION then
    cycle = cycle + 1;
$$end
  }
}
